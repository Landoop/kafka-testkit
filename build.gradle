buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath 'com.github.maiflai:gradle-scalatest:0.9'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
        classpath 'net.researchgate:gradle-release:2.3.4'
    }
}

ext {
    scalaMajorVersion = '2.11'
    junit = '4.12'
    scalaLoggingVersion = '2.1.2'
    kafkaVersion = '0.10.0.0'
    curatorVersion = '2.9.0'
    zkclientVersion = '0.7'
    confluentVersion = '3.0.0'
}

apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'net.researchgate.release'

allprojects {
    apply plugin: 'java'
    apply plugin: 'scala'
    apply plugin: 'maven'
    apply plugin: "com.github.maiflai.scalatest"
    apply plugin: 'com.github.johnrengelman.shadow'

    group = 'com.datamountaineer'
    version = version
    description = "kafka-testkit"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        maven { url "http://repo.maven.apache.org/maven2" }
        maven { url "http://packages.confluent.io/maven/" }
    }

//    configurations.all {
//        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
//            if (details.requested.name == 'log4j') {
//                details.useTarget "org.slf4j:slf4j-api:$slf4jVersion"
//            }
//        }
//    }
}

dependencies {
    compile "com.typesafe.scala-logging:scala-logging-slf4j_$scalaMajorVersion:$scalaLoggingVersion"
    compile "junit:junit:$junit"
    compile "com.101tec:zkclient:$zkclientVersion"
    compile "org.apache.kafka:kafka_$scalaMajorVersion:$kafkaVersion"
    compile "org.apache.kafka:kafka_$scalaMajorVersion:$kafkaVersion:test"
    compile "org.apache.curator:curator-test:$curatorVersion"
    compile "io.confluent:kafka-schema-registry:$confluentVersion"
}

//sourceSets.main.java.srcDirs = []
//sourceSets.test.java.srcDirs = []

test {
    maxParallelForks = 1
    minHeapSize '256m'
    maxHeapSize '2048m'
    systemProperty "log4j.configuration","${projectDir}/test/resources/log4j.properties"
}

task testJar(type: Jar, dependsOn: testClasses) {
    baseName = "test-${project.archivesBaseName}"
    from sourceSets.test.output
}

configurations {
    tests
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}


task scaladocJar(type: Jar) {
    classifier = 'scaladoc'
    from '../LICENSE'
    from scaladoc
}

artifacts {
    archives javadocJar, scaladocJar, sourcesJar
}

tasks.withType(Tar) {
    compression Compression.GZIP
    extension = 'tgz'
}

task compile(dependsOn: 'compileScala')
task fatJar(dependsOn : ['test', 'shadowJar', 'makeCli'])
task fatJarNoTest(dependsOn : 'shadowJar')

println "Building ${name}-${version}"

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

tasks.withType(Jar) {
    manifest {
        attributes 'Main-Class': 'com.datamountaineer.connect.tools.Cli'
    }
}

if (project.hasProperty('release')) {
    // OSSRH publication
    uploadArchives {
        repositories {
            mavenDeployer {
                // POM signature
                beforeDeployment { MavenDeployment deployment ->
                    signing.signPom(deployment)
                }
                // Target repository
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
                pom.project {
                    name project.name
                    description project.description
                    packaging 'jar'
                    url 'https://github.com/datamountaineer/kafka-connect-tools'

                    scm {
                        connection 'scm:git:https://github.com/datamountaineer/kafka-connect-tools.git'
                        developerConnection 'scm:git:git@github.com:datamountaineer/kafka-connect-tools.git'
                        url 'https://github.com/datamountaineer/kafka-connect-tools.git'
                    }

                    licenses {
                        license {
                            name 'Apache License 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.html'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'andrewstevenson'
                            name = 'Andrew Stevenson'
                            email = 'andrew@datamountaineer.com'
                        }
                        developer {
                            id = 'rollulus'
                            name = 'Roel'
                            email = 'rollulus@xs4all.nl'
                        }
                    }
                }
            }

        }
    }

}

nexusStaging {
    packageGroup = project.getGroup()
    username = ossrhUsername
    password = ossrhPassword
}